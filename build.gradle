plugins {
	id 'fabric-loom' version '0.4-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+mc${project.minecraft_version}"
group = project.maven_group

minecraft {
	refmapName = "${project.mod_id}-refmap.json"
}

repositories {
	maven { url "https://maven.fabricmc.net/" }
	maven { url "https://minecraft.curseforge.com/api/maven" }
	maven { url "https://jitpack.io/" }
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_mappings}"
	modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

	def fabric_deps = [
	        'fabric-api-base': '0.1.3+12a8474cb0',
			'fabric-networking-v0': '0.1.9+1a47cd78b0',
			'include': false,
			'fabric-registry-sync-v0': '0.3.8+7dba2d6cb0',
			'fabric-resource-loader-v0': '0.2.5+059ea866b0',
			'fabric-tag-extensions-v0': '0.2.5+5a6e8f4cb0'
	]

	def inc = true
	for (entry in fabric_deps.entrySet()) {
		if (entry.getKey() == "include") {
			inc = entry.getValue()
			continue
		}
        modImplementation 'net.fabricmc.fabric-api:' + entry.getKey() + ':' + entry.getValue()
		if (inc) {
			include 'net.fabricmc.fabric-api:' + entry.getKey() + ':' + entry.getValue()
		}
	}

	modCompileOnly("me.shedaniel:RoughlyEnoughItems:4+") {
		exclude group: "net.fabricmc.fabric-api"
	}

	modImplementation("com.github.Siphalor:ModsOfTheWorld:1.16-SNAPSHOT") {
		exclude group: "net.fabricmc.fabric-api"
	}
}

//sourceSets.main.java.exclude("de/siphalor/nbtcrafting/compat/**/*REI*")

processResources {
	inputs.property "version", version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
	classifier = ""
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}
